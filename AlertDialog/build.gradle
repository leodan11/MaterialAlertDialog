plugins {
    id 'com.android.library'
    id 'org.jetbrains.kotlin.android'
    id 'org.jetbrains.kotlin.kapt'
    id 'maven-publish'
}

android {
    namespace 'com.github.leodan11.alertdialog'
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
            withJavadocJar()
        }
    }
    buildFeatures {
        buildConfig = true
        dataBinding = true
        viewBinding = true
    }
    lint {
        abortOnError false
    }
}

dependencies {

    def k_extensions_version = "2.0.0"

    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    implementation 'com.airbnb.android:lottie:6.6.0' // Lottie for Android
    implementation "com.github.leodan11.KExtensions:core:$k_extensions_version" // KTX Extension
    implementation "com.github.leodan11.KExtensions:color:$k_extensions_version"
    implementation "com.github.leodan11.KExtensions:context:$k_extensions_version"
    implementation "com.github.leodan11.KExtensions:number:$k_extensions_version"
    implementation "com.github.leodan11.KExtensions:views:$k_extensions_version"
    implementation 'com.github.leodan11:OtpView:v2.0.4' // OtpView
    implementation 'com.github.leodan11:ReadMoreOptions:1.1.6' // ReadMoreTextView
    implementation 'io.github.vicmikhailau:MaskedEditText:5.0.3' // MaskedEditText
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.leodan11'
                artifactId = 'MaterialAlertDialog'
                version = '1.11.4'

                pom {
                    name = "MaterialAlertDialog"
                    description = "A beautiful, fluid, and extensible dialogs API for Kotlin & Android"
                    url = "https://github.com/leodan11/MaterialAlertDialog"
                }

            }
        }
    }
}
